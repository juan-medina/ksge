import org.gradle.internal.os.OperatingSystem

plugins {
    id 'org.jetbrains.kotlin.multiplatform'
    id 'idea'
}

apply plugin: 'idea'

def GROUP_ID = 'com.juanmedina'
def ARTIFACT_ID = 'kecs'

def LIBRARY_VERSION_NAME = '0.0.1'

group GROUP_ID
version LIBRARY_VERSION_NAME

kotlin {
    def platform = null

    if (OperatingSystem.current().isLinux()) {
        platform = linuxX64("linux") {
        }
    }

    if (OperatingSystem.current().isWindows()) {
        platform = mingwX64("mingw") {
        }
    }

    platform.compilations.main {
        cinterops {
            raylib {
                defFile rootProject.file("cinterop/raylib/raylib.def")
                compilerOpts "-I" + rootProject.file("cinterop/raylib")
            }
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                implementation 'com.juanmedina:kecs:1.0.2'
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        if (OperatingSystem.current().isLinux()) {
            linuxMain {
                dependencies {
                    implementation 'com.juanmedina:kecs-linux:1.0.2'
                }
                dependsOn commonMain
            }
            linuxTest {
                dependsOn commonTest
            }
        }
        if (OperatingSystem.current().isWindows()) {
            mingwMain {
                dependencies {
                    implementation 'com.juanmedina:kecs-mingw:1.0.2'
                }
                dependsOn commonMain
            }
            mingwTest {
                dependsOn commonTest
            }
        }
    }
}

